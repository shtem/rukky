program -> decl_list EOF
decl_list -> decl_list decl
            | decl
decl -> stmt 
        | func_decl

var_type -> "real"
            | "bool"
            | "string"
func_type -> "void"
            | var_type
            | var_type "[]"
func_decl -> func_type ID ":=" "(" params ")" block 
params -> param_list
        | epsilon
param_list -> param_list "," param
            | param
param -> var_type ID

block -> "{" EOL stmt_list "}" EOL
        | "{" EOL "}" EOL
        | "{}" EOL
stmt_list -> stmt_list stmt
            | stmt
stmt -> decl_stmt
        | expr_stmt
        | for_stmt
        | while_stmt
        | if_stmt
        | return_stmt
        | break_stmt
decl_stmt -> var_type ID EOL
            | var_type ID ":=" expr EOL
            | var_type "[]" ID EOL
            | var_type "[]" ID ":=" "[" args "]" EOL
expr_stmt -> expr EOL
            | EOL
for_stmt -> "for" "::" ID ":=" expr ":" expr ":" expr block
while_stmt -> "while" "::" expr block

if_stmt -> "if" "::" expr block elif_stmt_list else_stmt
elif_stmt_list -> elif_stmt_list elif_stmt
                | elif_stmt
elif_stmt -> "elif" "::" expr block
            | epsilon
else_stmt -> "else" "::" block
            | epsilon

return_stmt -> "return" ":" EOL
            | "return" ":" expr EOL
break_stmt -> "break" ":" EOL

expr -> ID ":=" expr
        | ID ":=" "[" args "]"
        | ID "@" expr ":=" expr
        | ID "<<" expr
        | disjunc
disjunc -> disjunc "||" conjunc
        | conjunc
conjunc -> conjunc "&&" equiv
        | equiv
equiv -> equiv "<>" ineq
        | equiv "<!" ineq
        | ineq
ineq -> ineq "<=" term
        | ineq "<" term
        | ineq ">=" term
        | ineq ">" term
        | term
term -> term "+" factor
        | term "-" factor
        | factor
factor -> factor "*" expo
        | factor "/" expo
        | factor "//" expo
        | factor "%" expo
        | expo
expo -> expo "^" elem 
        | elem
elem -> "-" elem
        | "~" elem
        | "(" expr ")"
        | ID
        | ID ":" args EOL
        | ID "[" expr "]"
        | REAL_LIT
        | BOOL_LIT
        | STRING_LIT
args -> arg_list
        | epsilon
arg_list -> arg_list "," expr
        | expr

epsilon -> 